#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT([git], [1.4.0], [git@vger.kernel.org])

AC_CONFIG_SRCDIR([git.c])

config_file=config.mak.autogen
config_append=config.mak.append
config_in=config.mak.in

echo "# ${config_append}.  Generated by configure." > "${config_append}"

# Definitions of macros
# GIT_APPEND_LINE(LINE)
# --------------------
# Append LINE to file ${config_append}
AC_DEFUN([GIT_APPEND_LINE],
[[echo "$1" >> "${config_append}"]])# GIT_APPEND_LINE

# GIT_PARSE_WITH(PACKAGE)
# ----------------------
# For use in AC_ARG_WITH action-if-found, for packages default ON. 
# * Set NO_PACKAGE=YesPlease  for --without-PACKAGE
# * Set PACKAGEDIR=ARG for --with-PACKAGE=ARG
# * Do nothing for --with-PACKAGE without ARG
# PACKAGE option must be all uppercase
AC_DEFUN([GIT_PARSE_WITH],
[[if test "$withval" = "no"; then \
    GIT_APPEND_LINE(NO_$1=YesPlease); \
  elif test "$withval" != "yes"; then \
    GIT_APPEND_LINE($1DIR=$withval); \
  fi; \
]])# GIT_PARSE_WITH


# Checks for libraries.
AC_MSG_NOTICE([CHECKS for libraries])
AC_CHECK_LIB([crypto], [SHA1_Init],,GIT_APPEND_LINE(NO_OPENSSL=YesPlease))
AC_CHECK_LIB([curl], [curl_easy_setopt],,GIT_APPEND_LINE(NO_CURL=YesPlease))
AC_CHECK_LIB([expat], [XML_ParserCreate],,GIT_APPEND_LINE(NO_EXPAT=YesPlease))


# Checks for typedefs, structures, and compiler characteristics.
AC_MSG_NOTICE([CHECKS for typedefs, structures, and compiler characteristics])

AC_CHECK_MEMBER(struct dirent.d_ino,,
GIT_APPEND_LINE(NO_D_INO_IN_DIRENT=YesPlease),
[#include <dirent.h>])
AC_CHECK_MEMBER(struct dirent.d_type,,
GIT_APPEND_LINE(NO_D_TYPE_IN_DIRENT=YesPlease),
[#include <dirent.h>])

AC_CHECK_TYPE(struct sockaddr_storage,,
GIT_APPEND_LINE(NO_SOCKADDR_STORAGE=YesPlease),
[#include <netinet/in.h>])


# Checks for library functions.
AC_MSG_NOTICE([CHECKS for library functions])
AC_CHECK_FUNC(strcasestr,,GIT_APPEND_LINE(NO_STRCASESTR=YesPlease))
AC_CHECK_FUNC(strlcpy,,GIT_APPEND_LINE(NO_STRLCPY=YesPlease))
AC_CHECK_FUNC(setenv,,GIT_APPEND_LINE(NO_SETENV=YesPlease))


# Site configuration
AC_MSG_NOTICE([CHECKS for site configuration])
AC_ARG_WITH(curl, 
AS_HELP_STRING([--with-curl],[support http(s):// transports (default is YES)])
AS_HELP_STRING([],           [ARG can be also prefix for curl library and headers]),\
GIT_PARSE_WITH(CURL))

AC_ARG_WITH(openssl,
AS_HELP_STRING([--with-openssl],[use OpenSSL library (default is YES)])
AS_HELP_STRING([],              [ARG can be prefix for openssl library and headers]),\
GIT_PARSE_WITH(OPENSSL))

AC_ARG_WITH(expat,
AS_HELP_STRING([--with-expat],[support git-push using http:// and https:// transports via WebDAV (default is YES)])
AS_HELP_STRING([],            [ARG can be also prefix for expat library and headers]),\
GIT_PARSE_WITH(EXPAT))


# Output files
AC_CONFIG_FILES(["${config_file}":"${config_in}":"${config_append}"])
AC_OUTPUT

rm -f "${config_append}"

grep -q -s -F -e "-include ${config_file}" config.mak || \
            echo "-include ${config_file}" >> config.mak
